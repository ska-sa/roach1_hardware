*
*    AD8320 Digitally Controlled Amplifier SPICE model, rev A

*      This file will model many of the characteristics of the
*    AD8320, both digital and analog. The bandwidth, and the
*    gain vs digital input word both match the real part nearly
*    exactly. The power down feature will shutdown the output,
*    as well as lower the operating current to the datasheet
*    spec, when the pdb pin is grounded. Vocm is internally
*    biased to VCC/2 and the output swing vs supply voltage is
*    within 0.1volts of the real part
  
*    For ease of AC analysis, a feature has been added to the
*    model that does not exist on the real AD8320. This is a
*    mux which allows the user to program the gain word in a
*    parallel format, without having to use the latch. Bringing
*    the sel pin in the model (which does not exist on the real
*    part) to a logic low allows the user to preset the gain
*    word. The gain word is defined by pins y1 to y8 (y8=msb) and
*    is set by standard logic levels.

*    The digital timing to set the gain word is close to that of
*    the real part. However, the user is cautioned that if the
*    model is used near the limits of digital timing minimums,
*    the results may not match those of the real part exactly.
*
*                datenb sel clk sdata vcc
*                  |     |   |    |   |  Vin
*                  |     |   |    |   |  | Vref
*                  |     |   |    |   |  | | vocm
*                  |     |   |    |   |  | | |  pdb
*                  |     |   |    |   |  | | |  |   Vout
*
.subckt ad8320an datenb sel clk sdata 99 7 6 97 pdb 98
+ y8 y7 y6 y5 y4 y3 y2 y1 

**** output section

rb1 101 94 150
rb2 0 94 150
gb1 101 94 poly(3) 5 4 97 0 99 0 0 13.34 6.67e-3 -3.33e-3
gb2 0 94 poly(3) 5 4 97 0 99 0 0 13.34 6.67e-3 -3.33e-3
ecl1 101 95 poly(1) 99 0 0.65 0.17
dcl1 94 95 dx
ecl2 96 0 poly(1) 99 0 0.65 0.17
dcl2 96 94 dx
vout1 101 99 0
vout2 94 98 0
rout1 98 0 1e4
rout2 99 98 1e4

gpwra 99 0 poly(2) 99 0 pdc 0 17.5e-3 0.96e-3 14e-3 0 0.42e-3 
fpwr 99 0 poly(2) vout1 vout2 0 1 1

rcm1 99 97 100k
rcm2 97 0 100k

**** input amp/attenuator

vref 6 0 1.9
rin 7 8 220
cin 7 0 2e-12
rfb 8 11 220
emid 9 0 6 8 1
ecomp 11 0 9 0 1e5
ebuf1 117 0 poly(2) 11 0 7 0 0 0.01 -0.01
ratt 117 118 1k
catt 118 0 1e-12
ebuf2 19 0 99 0 0.5
ebuf3 19 17 118 0 1
q1 4 17 3 qn1
q2 5 19 3 qn1
gpd 3 0 poly(1) pd 0 8.7333e-6 -2.37e-6
q3 4 17 13 qn1
q4 5 19 13 qn1
g1 13 0 poly(1) u1 0 -0.0528e-6 0.587e-6  
q5 4 17 23 qn1
q6 5 19 23 qn1
g2 23 0 poly(1) u2 0 -0.1043e-6 1.16e-6
q7 4 17 33 qn1
q8 5 19 33 qn1
g3 33 0 poly(1) u3 0 -0.2087e-6 2.32e-6
q9 4 17 43 qn1
q10 5 19 43 qn1
g4 43 0 poly(1) u4 0 -0.4166e-6 4.63e-6
q11 4 17 53 qn1
q12 5 19 53 qn1
g5 43 0 poly(1) u5 0  -0.8324e-6 9.25e-6
q13 4 17 63 qn1
q14 5 19 63 qn1
g6 43 0 poly(1) u6 0 -1.6648e-6 18.5e-6
q15 4 17 73 qn1
q16 5 19 73 qn1
g7 43 0 poly(1) u7 0 -3.3298e-6 37e-6
q17 4 17 83 qn1
q18 5 19 83 qn1
g8 43 0 poly(1) u8 0 -6.6596e-6 74e-6

r1 99 4 100
r2 99 5 100

**** digital logic section

x1 datenb datenb datenb daten nand
x2 daten clk clk clka nand
x3 sdata sdata sdata k1 nand
x4 clka sdata k1 q1 qb1 jkff
x5 clka q1 qb1 q2 qb2 jkff
x6 clka q2 qb2 q3 qb3 jkff
x7 clka q3 qb3 q4 qb4 jkff
x8 clka q4 qb4 q5 qb5 jkff
x9 clka q5 qb5 q6 qb6 jkff
x10 clka q6 qb6 q7 qb7 jkff
x11 clka q7 qb7 q8 qb8 jkff
x12 datenb q1 z1 dltch
x13 datenb q2 z2 dltch
x14 datenb q3 z3 dltch
x15 datenb q4 z4 dltch
x16 datenb q5 z5 dltch
x17 datenb q6 z6 dltch
x18 datenb q7 z7 dltch
x19 datenb q8 z8 dltch
x20 sel pdb z1 y1 u1 z2 y2 u2 z3 y3 u3 z4 y4 u4
+ z5 y5 u5 z6 y6 u6 z7 y7 u7 z8 y8 u8 mux
x21 pdb pdb pdb pd nand
x22 pd pd pd pdc nand

.ends ad8320an

.subckt mux sel pdb in1a in1b out1 in2a in2b out2 in3a in3b out3
+   in4a in4b out4 in5a in5b out5 in6a in6b out6 in7a in7b out7
+   in8a in8b out8

x1 in1a in1b pdb sel out1 minimux 
x2 in2a in2b pdb sel out2 minimux
x3 in3a in3b pdb sel out3 minimux 
x4 in4a in4b pdb sel out4 minimux 
x5 in5a in5b pdb sel out5 minimux 
x6 in6a in6b pdb sel out6 minimux 
x7 in7a in7b pdb sel out7 minimux 
x8 in8a in8b pdb sel out8 minimux
.ends mux

.subckt minimux ina inb pdb sel out
x1 sel ina pdb x nand
x2 selb inb pdb y nand
x3 x y y out nand
x5 sel sel sel selb nand
.ends minimux

.subckt jkff clk j k q qb  
+   optional: dpwr=$g_dpwr dgnd=$g_dgnd
+   params: mntymxdly=0 io_level=0
u1 jkff(1) dpwr dgnd
+   dpwr dpwr clk j k q qb
+   t_jkff IO_STD mntymxdly={mntymxdly} io_level={io_level}
.ends jkff

.subckt dltch g d q   
+   optional: dpwr=$g_dpwr dgnd=$g_dgnd
+   params: mntymxdly=0 io_level=0
u1 dltch(1) dpwr dgnd
+   dpwr dpwr g d q qb
+   t_dltch IO_STD mntymxdly={mntymxdly} io_level={io_level}
.ends dltch

.subckt nand a b c out 
+   optional: dpwr=$g_dpwr dgnd=$g_dgnd
+   params: mntymxdly=0 io_level=0
rsel dpwr a 1k
u1 nand(3) dpwr dgnd
+   a b c out
+   D_00 IO_STD mntymxdly={mntymxdly} io_level={io_level}
.ends nand

.subckt AtoD_STD a d dpwr dgnd params: CAPACITANCE=0
O0  A DGND DO74 DGTLNET=D IO_STD
C1  A DGND {CAPACITANCE+0.1pF}
D0      DGND    a       D74CLMP
D1      1       2       D74
D2      2       DGND    D74
R1      DPWR    3       4k
Q1      1       3       A       0       Q74 ; substrate should be DGND

.model d74 d()
.model d74clmp d()
.model q74 pnp()
.model DO74 doutput (
+       s0name="X"      s0vlo=0.8       s0vhi=2.0
+       s1name="0"      s1vlo=-1.5      s1vhi=0.8
+       s2name="R"      s2vlo=0.8       s2vhi=1.4
+       s3name="R"      s3vlo=1.3       s3vhi=2.0
+       s4name="X"      s4vlo=0.8       s4vhi=2.0
+       s5name="1"      s5vlo=2.0       s5vhi=7.0
+       s6name="F"      s6vlo=1.3       s6vhi=2.0
+       s7name="F"      s7vlo=0.8       s7vhi=1.4
+       )
.ends atod_std

.subckt DtoA_STD  D A  DPWR DGND
+       params: DRVL=0 DRVH=0 CAPACITANCE=0

N1  A DGND DPWR DIN74 DGTLNET=D IO_STD
C1  A DGND {CAPACITANCE+0.1pF}
.model DIN74 dinput (
+       s0name="0"      s0tsw=3.5ns     s0rlo=7.13      s0rhi=389 ; 7ohm,    0.09v
+       s1name="1"      s1tsw=5.5ns     s1rlo=467       s1rhi=200 ; 140ohm,  3.5v
+       s2name="X"      s2tsw=3.5ns     s2rlo=42.9      s2rhi=116 ; 31.3ohm, 1.35v
+       s3name="R"      s3tsw=3.5ns     s3rlo=42.9      s3rhi=116 ; 31.3ohm, 1.35v
+       s4name="F"      s4tsw=3.5ns     s4rlo=42.9      s4rhi=116 ; 31.3ohm, 1.35v
+       s5name="Z"      s5tsw=3.5ns     s5rlo=200K      s5rhi=200K
+       )
.ends  DtoA_STD

.subckt DIGIFPWR  AGND
+       optional: DPWR=$G_DPWR DGND=$G_DGND
+       params:   VOLTAGE=5.0v REFERENCE=0v

VDPWR  DPWR DGND  {VOLTAGE}
R1     DPWR AGND  1MEG
VDGND  DGND AGND  {REFERENCE}
R2     DGND AGND  1e-3
.model do74 d()
.ends digifpwr

.model D_00 ugate (tplhty=2ns tplhmx=2ns tphlty=2ns tphlmx=2ns)

.model IO_STD uio (
+       drvh=96.4       drvl=104
+       AtoD1="AtoD_STD"        AtoD2="AtoD_STD_NX"
+       AtoD3="AtoD_STD"        AtoD4="AtoD_STD_NX"
+       DtoA1="DtoA_STD"        DtoA2="DtoA_STD"
+       DtoA3="DtoA_STD"        DtoA4="DtoA_STD"
+       tswhl1=1ms          tswlh1=1ms
+       tswhl2=1ms          tswlh2=1ms
+       tswhl3=1ms          tswlh3=1ms
+       tswhl4=1ms          tswlh4=1ms
+       DIGPOWER="DIGIFPWR"
+       )

.model t_jkff ueff(
+ tpclkqlhmn=2ns tpclkqlhty=2ns tpclkqlhmx=2ns
+ tpclkqhlmn=2ns tpclkqhlty=2ns tpclkqhlmx=2ns)

.model t_dltch ugff (
+       twghmx=2ns     tsudgmx=2ns
+       thdgmx=5ns      tpgqlhty=2ns
+       tpgqlhmx=3ns   tpgqhlty=1ns
+       tpgqhlmx=2ns   tpdqlhty=2ns
+       tpdqlhmx=3ns   tpdqhlty=1ns
+       tpdqhlmx=3ns
+       )

.model qn1 npn()
.model dx d()
