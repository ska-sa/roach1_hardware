*****************
* This model was developed for Analog Devices by:
* AEI Systems, LLC
* 5777 W. Century Blvd., Suite 876
* Los Angeles, California  90045-5677
*
* The model is Copyright © 2006, AEi Systems, LLC. All Rights Reserved. 
*
* Users may not directly or indirectly display, re-sell or 
* re-distribute this model or any derivative work therefore 
* without the prior written consent of both AEi Systems and 
* Analog Devices. This model is subject to change without 
* notice. Neither Analog Devices nor AEi Systems is responsible 
* for updating this model.
*
* For more information regarding modeling services, model 
* libraries and simulation products, please call AEi Systems 
* at (310) 216-1144, or contact AEi Systems by 
* email: info@aeng.com. Or visit AEi Systems on the web 
* at http://www.AENG.com
*$
*     closed loop gain and phase vs bandwidth
*     output current and voltage limiting
*     offset voltage (is static, will not vary with vcm)
*     ibias (again, is static, will not vary with vcm)
*     slew rate and step response performance
*     (slew rate is based on 10-90% of step response)
*     current on output will be reflected to the supplies 
*     vnoise, referred to the input
*     inoise, referred to the input

*     distortion is not characterized

* Node assignments
*                non-inverting input
*                | inverting input
*                | | positive supply
*                | | |  negative supply
*                | | |  |    output
*                | | |  |    |     Power down
*                | | |  |    |     |
.SUBCKT AD8000   1 2 Vcc Vee Vout  PD
dsl1 98 16 _D1_mod 
.MODEL _D1_mod D
Q1 34 3 5 _Q 
.MODEL _Q PNP
Q2 43 5 4 _Q1 
.MODEL _Q1 NPN
Q4 34 6 4 _Q 
Q3 43 3 6 _Q1 
X1 Vcc Pdown Pd 0 46 28 SWhyste2
R15 Pdown 0 1meg
Cin1 1 98 1.06p
Cin2 2 98 1p
V1 4 2 DC=0
Fsl 98 16 v1 1
rnoise4 23 98 1
gbuf 98 13 15 98 0.01
f1 98 7 vsl 2
Rgain 7 98 2.5e5
Cgain 7 98 1.067p
dcl1 7 8 _Diode 
.MODEL _Diode D N=1
dcl2 9 7 _Diode 
dsl2 16 98 _D1_mod 
dsl3 16 17 _D1_mod 
dsl4 17 16 _D1_mod 
rsl 17 18 0.22
vsl 18 98 DC=0
Vcl1 Vcc 8 DC=1.67
Vcl2 9 Vee DC=1.67
Epole 14 98 7 98 1
rpole 14 15 1
cpole 15 98 110p
rnoise1 19 98 23.87m
vnoise1 19 98 DC=0
vnoise2 21 98 DC=0.53
dnoise1 21 19 _D8_mod
.MODEL _D8_mod D KF=1e-9
.MODEL _DI_mod D KF=20e-9
fnoise1 20 98 Vnoise1 1
rnoise2 20 98 1
rnoise3 22 98 25.5u
vnoise3 22 98 DC=0
vnoise4 24 98 DC=0.575
dnoise2 24 22 _DI_mod
fnoise2 23 98 vnoise3 1
rbuf 98 13 100
fcurr 98 40 voc 1
vcur1 26 98 DC=0
vcur2 98 27 DC=0
dcur2 27 40 _D1_mod 
dcur1 40 26 _D1_mod 
vo1 Vcc 90 DC=0
vo2 Vee 91 DC=0
gout1 90 11 13 Vcc 0.5
gout2 91 11 13 Vee 0.5
rout1 11 90 2
rout2 11 91 2
Voc 11 47 DC=0
rout3 47 98 1meg
dcl3 13 10 _D1_mod 
dcl4 12 13 _D1_mod 
Vcl3 10 11 DC=-0.445
Vcl4 11 12 DC=-0.445
EBeos 3 1 Value = {V(20,98) + 1M}
GBfbn 2 98 Value = {I(VNOISE3)*1M + 5U}
GBfbp 1 98 Value = {I(VNOISE3)*1M + 5U}
GBgcm 98 7 Value = {V(98)*10U + V(30)*10U}
EBeref 98 0 Value = {V(VCC)*0.5 + V(VEE)*0.5}
EBecmref 30 0 Value = {V(1)*0.5 + V(2)*0.5}
GBfout1 0 Vcc Value = {I(VO1) - I(VCUR1) - 8.8M}
GBfout2 Vee 0 Value = {I(VO2) - I(VCUR2) - 8.8M}
Vcmm 34 Vee DC=1.5
Vcmp Vcc 43 DC=1.5
Vh 28 0 DC=0.6
Vt Vcc 46 DC=2.5
GBIpowdn Vee Vcc Value = {IF(V(PDOWN) < .75*V(VCC) , 7.8M , 0)}
ERout VRVAR 0 Value={ IF (v(pdown) < .75*V(Vcc) , 1Meg , 1m)}
XRout 47 Vout VRVAR 0 Rvar
GBbias1 43 5 Value = {IF(V(PDOWN) < .75*V(VCC) , 0 , 1.625M)}
GBbias2 6 34 Value = {IF(V(PDOWN) < .75*V(VCC) , 0 , 1.625M)}
.ENDS
.SUBCKT SWHYSTE2 NODEMINUS NODEPLUS PLUS MINUS VT VH
S5 NODEPLUS NODEMINUS 8 0 SMOOTHSW
EBCRTL 8 0 Value = {IF(V(PLUS)-V(MINUS) > V(REF) , 1 , 0)}
EBREF REF1 0 Value = {IF(V(8) > 0.5 , V(VT)-V(VH) , V(VT)+V(VH))}
RDEL REF1 REF 100
CDEL REF 0 100P  IC=1.0000 
RCONV1 8 0 10MEG
RCONV2 PLUS 0 10MEG
RCONV3 MINUS 0 10MEG
.MODEL SMOOTHSW VSWITCH (RON=1.0000  ROFF=1.0000MEG VON=1 VOFF=0)
.ENDS 
*$
.subckt Rvar 101 102 201 202 
rin 201 202 1G
r 301 0 1
fcopy 0 301 vsense 1
eout 101 106 poly(2) 201 202 301 0 0 0 0 0 1
vsense 106 102 0; sense iout
.ends
*$
